plugins {
    id 'com.android.library'
    id 'kotlin-android'
    //添加这两行
    id 'com.github.dcendents.android-maven'
    id 'com.jfrog.bintray'
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.swift.sandhook:hooklib:4.2.1'
    // 不使用 Xposed API 则不需要引入
    implementation 'com.swift.sandhook:xposedcompat:4.2.1'
}

//上传到JCenter的其他配置
Properties config = new Properties()
config.load(project.rootProject.file('config.properties').newDataInputStream())

//项目主页
def siteUrl = 'https://github.com/15198184721/HookLibraryJCenter.git'
//项目的git地址
def gitUrl = 'https://github.com/15198184721/HookLibraryJCenter.git'
//发布到JCenter上的项目名字
def libName = config.getProperty("bintray.libName")

//发布到组织名称名字，必须填写
group = config.getProperty("bintray.group")
// 版本号，下次更新是只需要更改版本号即可
version = config.getProperty("bintray.version")
//上面配置后上传至JCenter后的编译路径是这样的：
// compile 'cn.lk.hook:SandHook:1.0.0'

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//上传到JCenter所需要的源码文件
artifacts {
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                //项目描述，随意填
                name '这个是Hook测试的项目。此主要是为了测试兼容性等'
                url siteUrl
                licenses {
                    license {
                        //开源协议
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        //开发者的个人信息
                        id config.getProperty("bintray.id")
                        name config.getProperty("bintray.name")
                        email config.getProperty("bintray.email")
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter用户配置
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        //这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = config.getProperty("bintray.repo")
        //发布到JCenter上的项目名字
        name = libName
        //项目描述
        desc = 'android 下的非root情况下的一些Hook方式。主要用于方法Hook'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}